/**
 * @file ex10_queue_1P_1C_empty_test.ino
 * @brief 1 producer (Sender)tasks with priority=1 and 1 customer (Receiver) task with priority=2
 * Producer writes periodicaly numeric data into queue
 * Customer wait until something is in the queue and displays it
 * FreeRTOS API reference : https://www.freertos.org/a00106.html 
 * @author philippe.camus@hepl.be from examples suite of FreeRTOS
 * @date 31/8/2021   
 */
#include <Arduino_FreeRTOS.h>
#include <queue.h>

//#define CHECK_TIMEOUT //Uncomment to check for timeout 

/* The tasks to be created.  Two instances are created of the sender task while
only a single instance is created of the receiver task. */
static void vSenderTask( void *pvParameters );
static void vReceiverTask( void *pvParameters );

/* Declare a variable of type QueueHandle_t.  This is used to store 
 a reference to the queue that is accessed by all three tasks. */
QueueHandle_t xQueue;


// the setup function runs once when you press reset or power the board
void setup() 
{
// initialize serial communication at 9600 bits per second:
Serial.begin(9600);

xQueue = xQueueCreate( 5, sizeof( uint32_t ) ); // 5 uint32_t elements
if( xQueue != NULL ) // if queue object is created
  {
   /* Create the task that will write to the queue with priority 1. */
   xTaskCreate( vProducerTask, "Producer", 200, NULL, 1, NULL ); 

   /* Create the task that will read from the queue.  The task is created with
   priority 2, so above the priority of the sender task. */
   xTaskCreate( vCustomerTask, "Consumer", 200, NULL, 2, NULL );
         
  //Start the scheduler so our tasks start executing.
  vTaskStartScheduler(); // not necessary in last implementation
                 // present in the initVariant() function of the Free RTOS library
  }
  else 
   Serial.println( "Could not create the queue." );
}

void loop()
{
  // Empty. Things are done in Tasks.
}

/*--------------------------------------------------*/
/*---------------------- Tasks ---------------------*/
/*--------------------------------------------------*/
static void vProducerTask( void *pvParameters )
{
uint32_t lValueToSend = 0UL;
uint32_t delay_send = 500;


//As always in embedded systems, this task is implemented in an infinite loop.
while(1)
  {
    /* The first parameter is the queue to which data is being sent.  The
    queue was created before the scheduler was started, so before this task
    started to execute.

    The second parameter is the address of the data to be sent.

    The third parameter is the Block time, the time the task should be kept
    in the Blocked state to wait for space to become available on the queue
    should the queue already be full.  In this case we will block indefinitively until the customer frees some data */
   xQueueSendToBack( xQueue, &lValueToSend, portMAX_DELAY );

   lValueToSend++;    
   
   #ifdef CHECK_TIMEOUT
   delay_send+=200;
   if (delay_send>= 1200)
     delay_send=500;

   vTaskDelay(delay_send/portTICK_PERIOD_MS);
   #endif
   
   #ifndef CHECK_TIMEOUT
   vTaskDelay(delay_send/portTICK_PERIOD_MS);
   #endif
  }
}
/*-----------------------------------------------------------*/

static void vCustomerTask( void *pvParameters )
{
/* Declare the variable that will hold the values received from the queue. */
uint32_t lReceivedValue;
BaseType_t xStatus;


//As always in embedded systems, this task is implemented in an infinite loop.
while(1)
  {
    /* The first parameter is the queue from which data is to be received.  The
    queue is created before the scheduler is started, and therefore before this
    task runs for the first time.

    The second parameter is the buffer into which the received data will be
    placed.  In this case the buffer is simply the address of a variable that
    has the required size to hold the received data.

    the last parameter is the block time, the maximum amount of time that the
    task should remain in the Blocked state to wait for data to be available should
    the queue already be empty. */
   xStatus = xQueueReceive( xQueue, (uint32_t *)&lReceivedValue, 1000 / portTICK_PERIOD_MS );

   if( xStatus == pdPASS )
     {
      /* Data was successfully received from the queue, print out the received
      value. */
      Serial.print( "Received = ");
      Serial.println( lReceivedValue );
     }
    else
     {
      /* We did not receive anything from the queue even after waiting for 1000ms.
      This must be an error as the sending task will be writing to the queue each 500 ms. */
      Serial.println( "No data received before timeout occurs" );
     }
  }
}
